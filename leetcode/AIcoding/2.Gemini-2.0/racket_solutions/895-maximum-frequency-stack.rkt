(define (make-FreqStack)
  (let ((freqs (make-hash))
        (groups (make-hash))
        (maxfreq 0))
    (define (push x)
      (let ((f (add1 (hash-ref freqs x 0))))
        (hash-set! freqs x f)
        (hash-update! groups f (lambda (xs) (cons x xs)) '())
        (set! maxfreq (max maxfreq f))))
    (define (pop)
      (let* ((x (car (hash-ref groups maxfreq)))
             (new-xs (cdr (hash-ref groups maxfreq))))
        (hash-set! groups maxfreq new-xs)
        (when (null? new-xs)
          (set! maxfreq (sub1 maxfreq)))
        (hash-update! freqs x sub1)
        x))
    (define (top)
      maxfreq)
    (define (dispatch m)
      (case m
        ((push) push)
        ((pop) pop)
        ((top) top)
        (else (error "Unknown method"))))
    dispatch))